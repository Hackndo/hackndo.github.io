---
title: "Tokens ERC20 et ERC721"
date: 2023-10-16 03:13:32
author: "Pixis"
layout: post
permalink: /tokens-standards/
disqus_identifier: 0000-0000-0000-00b9
cover: assets/uploads/2023/06/token_banner.png
description: "Une grande partie des applications décentralisées utilisent des tokens pour fonctionner correctement. Ce sont des jetons qui sont créés sur une blockchain déjà existante par l'intermédiaire de smart contracts. Ainsi, à l'aide d'un smart contract."
tags:
  - "Blockchain"
translation:
  - en
---

Une grande partie des applications décentralisées utilisent des **tokens** pour fonctionner correctement. Alors que les **coins** sont intrinsèques à chaque blockchain (Ether pour Ethereum, par exemple, Sol pour Solana, etc.), les **tokens** sont des jetons qui sont créés sur une blockchain déjà existante par l'intermédiaire de smart contracts. Ainsi, à l'aide d'un smart contract, il est possible de créer un token appelé "HackndoToken" dont le symbole serait "HKD", par exemple. Ce token pourrait exister en nombre limité, et nous pourrions même faire en sorte que chaque jeton HND soit unique.

<!--more-->

Ces tokens peuvent être transférés d'une adresse à l'autre, il peuvent être créés, détruits, gardés dans un "coffre", etc. Cependant, si chacun crée son token dans son coin, avec ses propres règles, ça deviendrait rapidement un joyeux bazar. Certains tokens pourraient avoir une fonction `transfer` pour transférer un token, d'autres pourraient utiliser `send()`, `sendTo()`, `transferToken()`, ou même `functionToTransferATokenToSomeoneLikeYouuuuu()`. Bref, on ne s'en sortirait pas. Il ne serait pas possible d'échanger un token contre un autre sans répertorier toutes les fonctions de tous les tokens existants.

C'est pourquoi, comme pour chaque technologie émergente, un standard doit être utilisé pour faciliter la communication entre applications, entre tokens. Ainsi, plusieurs améliorations de Ethereum ([Ethereum Improvement Proposal - EIP](https://github.com/ethereum/EIPs)) ont été proposées afin de définir différents standards de tokens en fonction des besoins des applications.


## Tokens fongibles - ERC20

La proposition d'amélioration [#20](https://github.com/ethereum/EIPs/issues/20) décrit un standard de token "classique". Cette proposition a été acceptée, et les détails de ce standard sont accessibles [à cette adresse](https://eips.ethereum.org/EIPS/eip-20). Comme c'était l'issue #20 qui était à l'origine de cette standardisation, on appelle ce standard **ERC20** (ERC pour _Ethereum Request for Comments_)

Quand je dis que c'est un token "classique", cela signifie que c'est un token avec les propriétés de base. Il a un nom, un symbole, et peut être transféré d'un adresse à une autre. Tous les tokens (du même type) sont équivalents, tout comme deux tickets de bus d'une même ville sont équivalents. Ces tickets, comme ces tokens (ou comme les Ethers), sont interchangeables. On les appelle alors des tokens **fongibles**.

En réalité, avoir un nom ou un symbole, ce n'est même pas obligatoire. C'est uniquement pratique pour les humains, pour aider à distinguer des tokens autrement que par leur adresse. C'est un peu comme des URL, bien plus pratique à retenir que des adresses IP. Ces deux informations, si elles sont utilisées, doivent être accessibles par les méthodes suivantes :

```java
function name() public view returns (string)
function symbol() public view returns (string)
```

Une autre information optionnelle peut être fournie, c'est le nombre de décimales supportées par le token. Si le token "HKD" a `8` décimales, alors pour avoir 1 HND, il faut en réalité en avoir 100 000 000 ! C'est comme l'euro, il faut 100 centimes pour avoir un euro. Il faudrait alors 10^8 fractions de HND pour avoir 1 HND. Ainsi, si un utilisateur a 150 000 000 HND, une application web indiquera qu'il en a `1.5` (`150 000 000 / 100 000 000`).

Cette information peut alors être accessible via la méthode suivante :

```java
function decimals() public view returns (uint8)
```

Outre ces trois informations qui sont plutôt d'ordre de la représentation, pour faciliter leur usage par des humains, ces jetons **doivent impérativement** implémenter les fonctions suivantes :

```java
// Cette fonction doit renvoyer le nombre total de jetons existants (qu'ils aient été distribués, ou non).
// S'il n'y a que 1 HND, avec 8 décimales, cette fonction renvoie 100 000 000.
function totalSupply() public view returns (uint256)

// Retourne le nombre de jetons possédés par une adresse.
function balanceOf(address _owner) public view returns (uint256 balance)

// Permet de transférer des jetons à une autre adresse.
function transfer(address _to, uint256 _value) public returns (bool success)

// Permet de transférer des jetons d'une adresse source vers une adresse de destination.
// Pour que cela fonctionne, il faut que l'adresse source ait préalablement autorisé celui
// qui effectue ce `transferFrom` à effectuer ce transfert de tokens (ça serait trop facile sinon ;))
function transferFrom(address _from, address _to, uint256 _value) public returns (bool success)

// C'est avec cette fonction qu'il est possible de déléguer à un compte la dépense d'un nombre
// défini de tokens. Cette fonction doit être appelée avant que le compte délégué ne puisse
// utiliser la fonction `transferFrom()`.
function approve(address _spender, uint256 _value) public returns (bool success)

// Cette fonction retournera le nombre de tokens qu'un compte peut dépenser au nom d'un autre compte.
function allowance(address _owner, address _spender) public view returns (uint256 remaining)
```

### Exemple

En ayant en tête toutes ces fonctions, il devient alors possible de créer un tout nouveau jeton depuis zéro avec Solidity !

> Attention, cet exemple est donné à titre indicatif. Il n'est absolument **pas adapté à de la production**.

```java
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

contract HackndoToken {

    // Lorsque des variables d'état sont déclarées publiques, des "getter" sont automatiquement créés par Solidity
    // Par exemple, on peut appeler la fonction HackndoToken.symbol();
    string public symbol;
    string public name;
    uint8 public decimals;
    uint256 public totalSupply;

    // Permet de savoir combien de tokens possède chaque adresse
    mapping(address => uint) private balances;

    // Permet de savoir quelle adresse a autorisé quelles adresses à dépenser combien de tokens en son nom
    // Par exemple :
    // allowed[adresse1][adresse2] = 10
    // Cela signifie que adresse2 peut dépenser 10 tokens de adresse1, à la place de adresse1.
    mapping(address => mapping(address => uint)) private allowed;

    // Des événements doivent être émis pour certaines actions
    event Transfer(address indexed from, address indexed to, uint tokens);
    event Approval(address indexed tokenOwner, address indexed spender, uint tokens);

    // Lors de la création du contrat, les informations concernant le tokens seront fournies
    constructor(string memory _symbol, string memory _name, uint8 _decimals, uint256 _totalSupply) {
        symbol = _symbol;
        name = _name;
        decimals = _decimals;
        totalSupply = _totalSupply;
        balances[msg.sender] = totalSupply;
        emit Transfer(address(0), msg.sender, _totalSupply);
    }

    // Retourne le nombre de tokens possédés par une adresse.
    function balanceOf(address _address) public view returns (uint balance) {
        return balances[_address];
    }


    // Permet de transférer des tokens à une autre adresse
    function transfer(address to, uint value) public returns (bool success) {
        // Pour transférer des tokens, il faut en avoir suffisamment
        require(balances[msg.sender] >= value, "INSUFFICIANT_FUNDS");
        
        // On supprime les tokens à celui qui les envoie
        balances[msg.sender] = balances[msg.sender] - value;

        // Et on les ajoute à celui qui les reçoit
        balances[to] = balances[to] + value;

        // Un événement est émis pour enregistrer le transfert
        emit Transfer(msg.sender, to, value);
        return true;
    }

    // Déléguer à un compte la dépense d'un nombre défini de tokens
    function approve(address spender, uint value) public returns (bool success) {
        // Celui qui appelle la fonction autorise "spender" à dépenser "value" tokens en son nom
        allowed[msg.sender][spender] = value;
        emit Approval(msg.sender, spender, value);
        return true;
    }

    // Dépenser des tokens à la place d'un autre compte, sous réserve que celui qui appelle la fonction ait été autorisé à le faire
    function transferFrom(address from, address to, uint value) public returns (bool success) {
        // On vérifie que celui qui appelle la fonction est autorisé à dépenser autant de tokens
        require(allowed[from][msg.sender] >= value, "NO_APPROVAL");

        // Puis on vérifie que le compte qui dépense les tokens en a suffisamment
        require(balances[from] >= value, "INSUFFICIANT_FUNDS");

        // L'autorisation est décrémentée du nombre de tokens utilisés
        allowed[from][msg.sender] = allowed[from][msg.sender] - value;

        // Puis on échange le nombre de tokens d'un compte à un autre
        balances[from] = balances[from] - value;
        balances[to] = balances[to] + value;
        emit Transfer(from, to, value);
        return true;
    }

    // Permet de savoir combien de token "spender" peut utiliser au nom de "tokenOwner"
    function allowance(address tokenOwner, address spender) public view returns (uint remaining) {
        return allowed[tokenOwner][spender];
    }
}
```

Ce contrat peut être compilé et déployé sur la blockchain Ethereum afin d'y créer un nouveau token. Incroyable n'est-ce pas ?

Ce type de token (cet exemple, ou un autre) peut représenter un peu ce qu'on veut. Ca peut être l'équivalent d'une somme d'argent dans un jeu vidéo, des points de compétence, des parts dans une entreprise (centralisée ou non), etc.

Dans cet exemple, nous avons écrit un token depuis zéro. Cependant, pour éviter les erreurs, et pour que la standardisation se passe au mieux, il est tout à fait possible de ne pas réinventer la route, et d'utiliser une version auditée et éprouvée, telle que celle [proposée par OpenZeppelin](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/ERC20.sol).

## Tokens non fongibles (NFT) - ERC721

Les **NFT** (Non-Fungible Tokens) sont une catégorie de tokens qui ont pour spécificité d'être **non fongibles**. Cela signifie que deux tokens, bien que provenant du même smart contract, sont différents. On peut comparer cette notion à des cartes qu'on peut collectionner. Bien que ce soit la même entreprise qui édite des cartes représentant, par exemple, les meilleurs hackers de la planète, chaque carte représente un hacker en particulier. Elle est certes de la même collection, mais n'est pas équivalente à une autre carte, représentant une autre personne.

Pour standardiser des tokens ayant cette notion d'**unicité**, une nouvelle demande d'amélioration de Ethereum a été faite, la [#721](https://github.com/ethereum/EIPs/issues/721), et ce nouveau standard est décrit dans la [documentation de Ethereum](https://ethereum.org/fr/developers/docs/standards/tokens/erc-721/), et est appelé **ERC721**.

Afin de s'assurer que chaque token provenant du même smart contract est unique, une nouvelle variable est introduite, `tokenId`. Cette variable doit être unique pour chaque token d'un smart contract afin de suivant le standard **ERC721**.

En plus de cette variable, les méthodes suivantes doivent être implémentées :

```java
// Permet de savoir combien de NFT possède un compte
function balanceOf(address _owner) external view returns (uint256);

// Permet de déterminer le propriétaire d'un NFT en particulier
function ownerOf(uint256 _tokenId) external view returns (address);

// Envoyer un NFT d'une adresse vers une autre, tout en s'assurant que l'adresse
// de destination est soit un compte EOA, soit un contrat qui sait gérer les NFT
// On va expliquer comment dans la suite de cet article
function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes data) external payable;
function safeTransferFrom(address _from, address _to, uint256 _tokenId) external payable;

// Envoyer un NFT d'une adresse vers une autre
function transferFrom(address _from, address _to, uint256 _tokenId) external payable;

// Autoriser un compte à gérer un NFT en son nom
function approve(address _approved, uint256 _tokenId) external payable;

// Autoriser un compte à gérer TOUS ses NFTs en son nom
function setApprovalForAll(address _operator, bool _approved) external;

// Vérifier qui peut gérer un NFT en particulier
function getApproved(uint256 _tokenId) external view returns (address);

// Vérifier si un compte bénéficie d'une totale délégation pour les NFTs d'un autre compte
function isApprovedForAll(address _owner, address _operator) external view returns (bool);
```

On retrouve quelques fonctions ressemblant de près ou de loin aux fonctions d'un token ERC20 (`balanceOf`, `transferFrom`, `approve` par exemple). Cependant, deux autres méthodes méritent un peu plus de détails.

### safeTransferFrom

La première, c'est `safeTransferFrom()`. Cette méthode existe pour éviter que des NFT ne soient envoyés à des contrats ne sachant pas gérer des NFT. Si c'était le cas, le contrat de destination n'ayant pas été créé pour gérer des NFT, aucune fonction ne permettrait de gérer le NFT nouvellement reçu. Cela signifierait que ce NFT ne pourrait pas être acheté par quelqu'un, ou récupéré de quelconque manière que ce soit. Il serait bloqué dans ce contrat _at vitam_. On imagine très bien que, lorsqu'on propose une collection limitée de quelque chose, on souhaite éviter d'en perdre dans la nature, inutilisables, non échangeables.

Pour éviter ce genre de problème, lorsque la fonction `safeTransferFrom()` est appelée pour envoyer des tokens à un contrat, le contrat de destination doit impérativement avec une fonction spéciale, `onERC721Received`

```java
function onERC721Received(
  address operator,
  address from,
  uint256 tokenId,
  bytes calldata data
) external returns (bytes4);
```

Cette fonction va être appelée par le contrat du token, et s'attend à une réponse très spécifique. Si cette fonction n'existe pas dans le contrat de destination (ou si la fonction existe mais ne retourne pas ce qui est attendu) alors le transfert de NFT va être annulé. Ainsi, pour recevoir des NFT via `safeTransferFrom`, un contrat doit avoir explicitement prévu cette fonction. Comme cette fonction n'existe que pour valider un `safeTransferFrom`, en règle générale, si un contrat a prévu cette fonction, c'est qu'il est capable par ailleurs de gérer des NFT.

[![onERC721Received](/assets/uploads/2023/06/onERC721Received.png)](/assets/uploads/2023/06/onERC721Received.png)

> La présence de `onERC721Received` n'est pas une garantie que le contrat sache gérer des NFT. On pourrait très bien créer un contrat qui implémente uniquement `onERC721Received`, et rien d'autre. Cet appel à cette callback est plutôt une sorte de garde-fou pour éviter des erreurs bêtes.

### setApprovalForAll

L'autre fonction qui mérite un point d'attention est `setApprovalForAll`, tout simplement parce qu'elle peut être dangereuse. En effet, lorsqu'un utilisateur utilise cette fonction pour approuver une adresse de destination, ça permet à la destination de gérer **TOUTE** la collection de NFT de l'utilisateur. Quand on parle de "gérer", ça veut dire que la destination peut envoyer les NFT de l'utilisateur à des adresses de destination arbitraires. Il pourrait les envoyer à l'adresse nulle (`0x0`), ce qui ferait perdre ces NFT pour toujours, ou même se les envoyer à lui même. Une fois le transfert terminé, l'utilisateur n'a aucun moyen de les récupérer.

Cette fonction, dangereuse, est donc à n'utiliser qu'en cas d'absolue confiance en le destinataire (si c'est un EOA) ou absolue compréhension du code (si la destination est un smart contract).

### Exemple

Voici un exemple d'implémentation de ERC721 _from scratch_ permettant de voir une implémentation simpliste des fonctions.

> Attention, cet exemple est donné à titre indicatif. Il n'est absolument **pas adapté à de la production**.

```java
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

// On doit déclarer cette interface pour pouvoir appeler la callback lors
// d'un safeTransferFrom
interface IERC721Receiver {
    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4);
}

contract HackndoToken {

    // Lorsque des variables d'état sont déclarées et publiques, des "getter" sont automatiquement créés par Solidity
    string public symbol;
    string public name;

    uint private tokenId;

    // A chaque NFT, on associe un propriétaire
    mapping(uint256 => address) private owners;

    // A chaque adresse, on associe le nombre de NFT qu'il possède
    mapping(address => uint256) private balances;

    // Permet de savoir quelle adresse a des droits de délégation sur quel NFT
    mapping(uint256 => address) private tokenApprovals;

    // Adresses ayant tous les droits sur les NFT d'autres adresses
    mapping(address => mapping(address => bool)) private operatorApprovals;


    // Lors de la création du contrat, les informations concernant le NFT seront fournies
    constructor(string memory _symbol, string memory _name) {
        symbol = _symbol;
        name = _name;
    }

    // Cette fonction n'est pas dans le standard, mais elle permet de créer des
    // NFT à volonté, sans restrictions ! Faites vous plaisir :)
    function mint(address _to) public {
        owners[tokenId] = _to;
        balances[_to]++;
        // Comme un crée un NFT, il faut incrémenter l'identifiant
        // pour que le prochain NFT soit différent
        tokenId++;
    }

    // Permet de savoir combien de NFT possède un compte
    function balanceOf(address _address) public view returns (uint balance) {
        return balances[_address];
    }

    // Permet de déterminer le propriétaire d'un NFT en particulier
    function ownerOf(uint256 _tokenId) external view returns (address) {
        // On ne peut pas avoir un tokenId plus grand que le dernier créé
        require(_tokenId < tokenId, "NOT_EXISTANT");
        return owners[_tokenId];
    }

    // Envoyer un NFT d'une adresse vers une autre, tout en s'assurant que l'adresse
    // de destination est soit un compte EOA, soit un contrat qui sait gérer les NFT
    function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes memory data) public payable {
        transferFrom(_from, _to, _tokenId);

        // Si c'est un contrat, on appelle la callback onERC721Received()
        // S'il y a une erreur, ou si la valeur de retour n'est pas celle attendue, on revert();
        // -> S'il y a un revert(), alors la fonction transferFrom() précédemment appelée sera annulée
        if (_to.code.length > 0) {
            try IERC721Receiver(_to).onERC721Received(msg.sender, _from, _tokenId, data) returns (bytes4 retval) {
                if (retval != IERC721Receiver.onERC721Received.selector) {
                    revert();
                }
            } catch (bytes memory) {
                revert();
            }
        }

    }

    function safeTransferFrom(address _from, address _to, uint256 _tokenId) external payable {
        safeTransferFrom(_from, _to, _tokenId, "");
    }

    // Envoyer un NFT d'une adresse vers une autre
    function transferFrom(address _from, address _to, uint256 _tokenId) public payable {
        // Soit il est propriétaire du NFT, soit il a été approuvé pour ce NFT, soit il a été approuvé pour tous les NFT (dont celui-ci) d'un autre compte
        require(owners[_tokenId] == msg.sender || tokenApprovals[_tokenId] == msg.sender || operatorApprovals[_from][msg.sender], "NO_APPROVAL");
        // Il faut que la source du transfert soit propriétaire du NFT
        require(owners[_tokenId] == _from, "NOT_OWNER");
        owners[_tokenId] = _to;
    }

    // Autoriser un compte à gérer un NFT en son nom
    function approve(address _approved, uint256 _tokenId) external payable {
        require(owners[_tokenId] == msg.sender, "NOT_OWNER");
        tokenApprovals[_tokenId] = _approved;
    }

    // Autoriser un compte à gérer TOUS ses NFTs en son nom
    function setApprovalForAll(address _operator, bool _approved) external {
        operatorApprovals[msg.sender][_operator] = _approved;
    }

    // Vérifier qui peut gérer ce NFT en particulier
    function getApproved(uint256 _tokenId) external view returns (address) {
        return tokenApprovals[_tokenId];
    }

    // Vérifier si un compte a une totale délégation pour les NFTs d'un autre compte
    function isApprovedForAll(address _owner, address _operator) external view returns (bool) {
        return operatorApprovals[_owner][_operator];
    }
}
```

Tout comme avec ERC20, il existe une version de [ERC721](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC721/ERC721.sol) proposée par OpenZeppelin qui permet de ne pas tout réinventer, et d'utiliser une base de code solide et éprouvée. 

# Conclusion

Ces deux standards sont les plus connus, mais ils sont loin d'être les seuls existants. En effet, les tokens peuvent être utilisés pour tellement d'applications que des standards se développent (et parfois meurent) à mesure que de nouvelles idées d'utilisation sont mises en avant.

Bien comprendre comment fonctionnent ces tokens est essentiel pour tout bon auditeur, puisqu'ils sont extrêmement courants dans les applications décentralisées, ou dApps.